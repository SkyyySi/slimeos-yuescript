import awful, gears, wibox, ruled, beautiful, menubar, naughty from require("awesome_std")

import Object, util, wrap from require("slimeos.lib")
import Button from require("slimeos.widgets")

button_wrapper = (widget, buttons) ->
	{
		{
			{
				widget
				margins: util.scale(4)
				widget:  wibox.container.margin
			}
			shape: gears.shape.circle
			buttons: buttons
			widget: Button
		}
		margins: util.scale(6)
		widget:  wibox.container.margin
	}

button_box_wrapper = (widgets) ->
	{
		{
			widgets
			spacing: util.scale(4)
			layout: wibox.layout.fixed.horizontal
		}
		margins: util.scale(6)
		widget:  wibox.container.margin
	}

-- {{{ Titlebars
-- Add a titlebar if titlebars_enabled is set to true in the rules.
client.connect_signal("request::titlebars", (c) ->
	menu = awful.menu {
		items: {
			{ "Close",        (() -> c::kill()), beautiful.titlebar_close_button_focus }
			{ "Maximize",     (() -> c.maximized = not c.maximized), beautiful.titlebar_maximized_button_focus_inactive },
			{ "Minimize",     (() -> c.minimized = not c.minimized), beautiful.titlebar_minimize_button_focus },
			{ "Sticky",       (() -> c.sticky = not c.sticky), beautiful.titlebar_sticky_button_focus_inactive },
			{ "Float / tile", (() -> c.floating = not c.floating), beautiful.titlebar_floating_button_focus_active },
			{ "Keep above",   (() ->
				c.below = false
				c.above = not c.above
			), beautiful.titlebar_ontop_button_focus_active },
			{ "Keep below",   (() ->
				c.above = false
				c.below = not c.below
			), beautiful.titlebar_below_button_focus_active }, -- Note: this icon doesn't exist in the deault icon set
		}
	}

	-- buttons for the titlebar
	buttons = {
		awful.button({}, 1, () ->
			c::activate {
				context: "titlebar"
				action:  "mouse_move"
			}
		)

		awful.button({}, 3, () ->
			c::activate {
				context: "titlebar"
				action:  "mouse_resize"
			}
		)

		awful.button({}, 2, nil, () -> menu::toggle())
	}

	c.titlebars = {}
	c.titlebars.top = awful.titlebar(c, {
		bg: gears.color.transparent
		shape: shape: util.shape(gears.shape.partially_rounded_rect, true, true, false, false, util.scale(15))
	})

	c.titlebars.top.widget = {
		{
			{
				button_wrapper(awful.titlebar.widget.iconwidget(c), {
					awful.button({}, 1, nil, () -> menu::toggle())
					awful.button({}, 2, nil, () -> menu::toggle())
				})
				{
					font:    beautiful.titlebar_font ?? beautiful.bold_font ?? beautiful.font .. " Bold"
					halign:  "center"
					buttons: buttons
					widget:  awful.titlebar.widget.titlewidget(c)
				}
				spacing: util.scale(2)
				layout:  wibox.layout.fixed.horizontal
			}
			{
				buttons: buttons
				layout:  wibox.layout.flex.horizontal
			}
			{
				--awful.titlebar.widget.floatingbutton(c)
				--awful.titlebar.widget.stickybutton(c)
				--awful.titlebar.widget.ontopbutton(c)
				button_box_wrapper {
					awful.titlebar.widget.minimizebutton(c)
					awful.titlebar.widget.maximizedbutton(c)
					awful.titlebar.widget.closebutton(c)
				}
				layout: wibox.layout.fixed.horizontal
			}
			layout: wibox.layout.align.horizontal
		}
		id: "background_role"
		bg: beautiful.bg_normal
		shape: util.shape(gears.shape.partially_rounded_rect, true, true, false, false, util.scale(15))
		widget: wibox.container.background
	}
)
-- }}}
